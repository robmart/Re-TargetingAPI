buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        maven {
            url = "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "com.wynprice.cursemaven:CurseMaven:2.1.1"
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

apply plugin: "com.matthewprenger.cursegradle"
apply plugin: "com.wynprice.cursemaven"

ext.configFile = file('gradle.properties')
ext.privFile = file('private.properties')

ext.config = parseConfig(configFile)
ext.priv = parseConfig(privFile)

version = "${config.mc_version}-${config.mod_version}"
group= "robmart.mods.targetingapi"
archivesBaseName = config.mod_name
author = config.author

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20200514-1.15.1'

    //makeObfSourceJar = true //throws error

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                targetingapi {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                targetingapi {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 're-targeting_api', '--all', '--output', file('src/generated/resources/')

            mods {
                targetingapi {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.15.2-31.2.31'
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mods.toml'

        expand 'version':project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mods.toml'
    }
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
                "Specification-Title": "${config.mod_name}",
                "Specification-Vendor": "${config.author}",
                "Specification-Version": "${config.mod_version}",
                "Implementation-Title": project.name,
                "Implementation-Version": "${config.mod_version}",
                "Implementation-Vendor": "${config.author}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task srcJar(type: Jar) {
    from(sourceSets.main.java)
    classifier = 'sources'
    archiveName = "${baseName}-${version}-sources.jar"
}

curseforge {
    if (priv == null) return
    apiKey = priv.curseforge_api_key

    project {
        id = '360655'
        changelog = 'Changes'
        releaseType = 'release'

        mainArtifact jar
        addArtifact srcJar
    }
    options {
        debug = false
    }
}

def parseConfig(File config) {
    if(config.exists()){
        config.withReader {
            def prop = new Properties()
            prop.load(it)
            return (new ConfigSlurper().parse(prop))
        }
    } else {
        return null
    }
}